<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="Hyphenations" script:language="StarBasic" script:moduleType="normal">Sub convertHyphInDoc()
	Dim statusIndicator as Object
	statusIndicator = ThisComponent.getCurrentController.StatusIndicator
	statusIndicator.Start("Конвертация переносов, подождите",10)
	convertHyphInText(ThisComponent.Text)
'	Globalscope.BasicLibraries.LoadLibrary( "MRILib" )
	statusIndicator.Start("Конвертация переносов, подождите",70)
	allNotes = ThisComponent.FootNotes
	For x = 0 to allNotes.Count -1
	  aNote = allNotes.getByIndex(x)
	  convertHyphInText(aNote.Text)
	Next
	'disableAutoHyph()
	statusIndicator.end()
End Sub

sub disableAutoHyph()
	Dim propertySetInfo As Object
	Dim oPositionOfMatch As Long
	oFamilies = ThisComponent.StyleFamilies
	sElements() = oFamilies.getElementNames()
	  oFamily = oFamilies.getByName(sElements(1))
	  For j = 0 to oFamily.getCount -1
	    oStyle = oFamily.getByIndex(j)
		propertySetInfo = oStyle.getPropertySetInfo()
	    If propertySetInfo.hasPropertyByName("ParaIsHyphenation") Then
	      oStyle.ParaIsHyphenation = false
	    EndIf
	  Next
End Sub

Sub convertHyphInText(textElement)
	Dim enum1Element As Object
	Dim enum1 As Object
	Dim i As Integer
	Dim cell As Object
	enum1 = textElement.createEnumeration()
	While enum1.hasMoreElements
		enum1Element = enum1.nextElement
		If enum1Element.supportsService("com.sun.star.text.Paragraph") Then
			convertParaHyphens(enum1Element)
		ElseIf enum1Element.supportsService("com.sun.star.text.TextTable") Then
			cellNames = enum1Element.cellNames
			For i = LBound(cellNames) To Ubound(cellNames)
				cell = enum1Element.getCellByName(cellNames(i))
				cellText = cell.getText()
				convertHyphInText(cellText)
		    Next i
		Else 
		EndIf
	Wend 
End Sub

Sub convertParaHyphens(para)
	Dim lineEnd As String
	Dim conversionTargets() As Object
	'No hyphenation needed
	If para.ParaIsHyphenation = false Then
		Exit Sub
	EndIf
	vCurs = ThisComponent.currentController.getViewCursor()
	vCurs.goToRange(para.Anchor,false)
	tCurs = vCurs.Text.createTextCursorByRange(vCurs)
	tCurs.goToStartOfParagraph(false)
	vCurs.goToRange(tCurs,false)

	Do While NOT tCurs.isEndOfParagraph()
		vCurs.gotoEndOfLine(false)
		tCurs.goToRange(vCurs,false)
		If (tCurs.isEndOfParagraph()) Then
			Exit Do
		EndIf
		tCurs.goLeft(1,true)
		lastChar = tCurs.getString()
		tCurs.goRight(1,false)
		tCurs.goRight(1,true)
		newChar = tCurs.getString()
		tCurs.goLeft(1,false)
		If needHyphen(lastChar,newChar) Then
			addToArray(conversionTargets(),tCurs.Text.createTextCursorByRange(tCurs))
			'replaceHyphen(tCurs,vCurs)
		ElseIf needLineBreak(lastChar,newChar) Then
			insertLineBreak(tCurs)
		EndIf
		tCurs.goRight(1,false)
		vCurs.goToRange(tCurs,false)
	Loop
	
	If  Ubound(conversionTargets) &lt;&gt; -1 Then
		para.ParaIsHyphenation = false
		For i = 0 To (Ubound(conversionTargets))
			replaceHyphen(conversionTargets(i))
		Next i
	EndIf

End Sub
Sub replaceHyphen(tCurs)
	vCurs = ThisComponent.currentController.getViewCursor()
	'insert soft hyphen character U+00AD
	tCurs.Text.insertControlCharacter(tCurs.End,com.sun.star.text.ControlCharacter.SOFT_HYPHEN,true)
	vCurs.goToRange(tCurs.End,false)
	If vCurs.isAtEndOfLine() = false AND vCurs.isAtStartOfLine() = false Then
		'backspace
		'insert hyphen character U+2010
		'tCurs.Text.insertString(tCurs.End,"‐",False)
		
		vCurs.goToRange(tCurs,false)
		Do While vCurs.isAtEndOfLine() = false AND vCurs.isAtStartOfLine() = false
			tCurs.goLeft(1,true)
			tCurs.CharScaleWidth = tCurs.CharScaleWidth - 5
			tCurs.goRight(1,false)
			vCurs.goToRange(tCurs,false)
			If tCurs.CharScaleWidth &lt; 80 Then
				tCurs.CharScaleWidth = 100
				Exit Do
			EndIf
		Loop
		
		vCurs.goToStartOfLine(true)
		tLine = vCurs.Text.createTextCursorByRange(vCurs)
		vCurs.goToRange(tCurs,false)
		tLine.CharScaleWidth = 100
		Do While vCurs.isAtEndOfLine() = false AND vCurs.isAtStartOfLine() = false
			tLine.CharScaleWidth = tLine.CharScaleWidth - 5
			vCurs.goToRange(tCurs,false)
			If tLine.CharScaleWidth &lt; 50 Then
				Exit Do
			EndIf
		Loop

		If vCurs.isAtEndOfLine() = false AND vCurs.isAtStartOfLine() = false Then
			'backspace
			
			MsgBox "Что-то пошло не так. Обратитесь к автору данного макроса."
		EndIf
	EndIf

End Sub

Function needLineBreak(before,after)
	needLineBreak = false
	If before = "/" Then
		If after &lt;&gt; " " AND after &lt;&gt; Chr(10) Then
			needLineBreak = true
		EndIf
	EndIf
End Function

Sub	insertLineBreak(tCurs)
	tCurs.Text.insertControlCharacter(tCurs.End,com.sun.star.text.ControlCharacter.LINE_BREAK,False)
End Sub



Function needHyphen(before, after)
	needHyphen = true
	'MsgBox Asc(newChar)
	Select Case before
		Case "."
			needHyphen = false
		Case "!"
			needHyphen = false
		Case "?"
			needHyphen = false
		Case "/"
			needHyphen = false
		Case " "
			needHyphen = false
		'U+002D Hyphen minus
		Case "-"
			needHyphen = false
		'U+2010  Hyphen
		Case "‐"
			needHyphen = false
		Case "­"
			needHyphen = false
		'U+2014 Em dash
		Case "—"
			needHyphen = false
		'U+2013 En dash
		Case "–"
			needHyphen = false
		'U+2012 Figure Dash 
		Case "‒"
			needHyphen = false
	End Select
	Select Case after
		Case "."
			needHyphen = false
		Case "!"
			needHyphen = false
		Case "/"
			needHyphen = false
		Case "?"
			needHyphen = false
		Case " "
			needHyphen = false
		Case "-"
			needHyphen = false
		Case "­"
			needHyphen = false
		'newline U+000A
		Case Chr(10)
			needHyphen = false
	End Select
End Function
</script:module>
