<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="Libraries" script:language="StarBasic">Sub updateLibs()
	AppDocLibToGlobal("Publishing")
End Sub


Sub AppLibToDocLib(sLibName$)
Dim oGlobalLib
oGlobalLib = GlobalScope.BasicLibraries
AddOneLib(sLibName, sLibName, oGlobalLib, BasicLibraries, True)
oGlobalLib = GlobalScope.DialogLibraries
AddOneLib(sLibName, sLibName, oGlobalLib, DialogLibraries, True)
End Sub

Sub AppDocLibToGlobal(sLibName$)
Dim oGlobalLib
oGlobalLib = GlobalScope.BasicLibraries
AddOneLib(sLibName, sLibName, BasicLibraries , oGlobalLib , True)
oGlobalLib = GlobalScope.DialogLibraries
AddOneLib(sLibName, sLibName, DialogLibraries, oGlobalLib , True)
End Sub

REM sSrcLib is the name of the source library contained in oSrcLibs
REM sDestLib is the name of the destination library contained in oDestLibs
REM oSrcLibs is the source library container
REM oDestLibs is the destination library container
REM if bClearDest is True, then the destination library is cleared
Sub AddOneLib(sSrcLib$, sDestLib$, oSrcLibs, oDestLibs, bClearDest As Boolean)
Dim oSrcLib
'The source library to copy
Dim oDestLib 'The destination library to receive the modules in oSrcLib
Dim sNames
Dim i%
REM If there is no destination library then simply return
If IsNull(oDestLibs) OR IsEmpty(oDestLibs) Then
Exit Sub
End If
REM Clear the destination library if requested
If bClearDest AND oDestLibs.hasByName(sDestLib) Then
oDestLibs.removeLibrary(sDestLib)
End If
REM If there is no source library, then there is nothing else to do
If IsNull(oSrcLibs) OR IsEmpty(oSrcLibs) Then
Exit Sub
End If
REM If the source library does not exist, then there is nothing else to do
If NOT oSrcLibs.hasByName(sSrcLib) Then
Exit Sub
End If
REM If the destination library does not exist, then create it
If NOT oDestLibs.hasByName(sDestLib) Then
oDestLibs.createLibrary(sDestLib)
End If
REM This is where the real fun begins!
REM It may seem obvious, but the libraries must be loaded first.
REM Common mistake to not load the libraries first!
oSrcLibs.loadLibrary(sSrcLib)
oDestLibs.loadLibrary(sDestLib)
REM Get the source and destination libraries
REM Get all of the contained modules that should be copied
oSrcLib = oSrcLibs.getByName(sSrcLib)
oDestLib = oDestLibs.getByName(sDestLib)
sNames = oSrcLib.getElementNames()
REM For each module, either add it or replace it
For i = LBound(sNames) To UBound(sNames)
If oDestLib.hasByName(sNames(i)) Then
oDestLib.replaceByName(sNames(i), oSrcLib.getByName(sNames(i)))
Else
oDestLib.insertByName(sNames(i), oSrcLib.getByName(sNames(i)))
End If
Next
End Sub

Sub CreateAGlobalLib
Dim oLib
Dim s$
If GlobalScope.BasicLibraries.hasByName("TestLib") Then
GlobalScope.BasicLibraries.removeLibrary("TestLib")
GlobalScope.DialogLibraries.removeLibrary("TestLib")
MsgBox "Deleted TestLib"
Else
GlobalScope.BasicLibraries.createLibrary("TestLib")
GlobalScope.DialogLibraries.createLibrary("TestLib")
oLib = GlobalScope.BasicLibraries.getByName("TestLib")
s = "Sub Main" &amp; CHR$(10) &amp;_
" x = x + 1" &amp; CHR$(10) &amp;_
"End Sub"
oLib.insertByName("Module1", s)
s = "=== Basic Libs ===" &amp; CHR$(10)
oLib = GlobalScope.BasicLibraries.getByName("TestLib")
s = s &amp; Join(oLib.getElementNames(), CHR$(10))
oLib = GlobalScope.DialogLibraries.getByName("TestLib")
s = s &amp; CHR$(10) &amp; CHR$(10) &amp; "=== Dialog Libs ===" &amp; CHR$(10)
s = s &amp; Join(oLib.getElementNames(), CHR$(10))
MsgBox s, 0, "Modules in the TestLib Library"
oLib = GlobalScope.BasicLibraries.getByName("TestLib")
MsgBox oLib.getByName("Module1"), 0, "Module1 in TestLib"
End If
End Sub





</script:module>
